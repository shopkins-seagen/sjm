%include 'init.sas';

data x0;
   length sql_flag indent 8 lastword firstword $ 12 sas_code line_out $ 1000;
   infile "&currdir.\test.sas" truncover;
   input @1 sas_code $1000.;
   retain sql_flag indent 0;
   array lv(*) $ 300 lv01-lv20;
   do i=1 to dim(lv);
      lv(i)='';
   end;
   if not missing(sas_code) then do;
      sas_code=tranwrd(sas_code,';',';'||'~');
      sas_code=tranwrd(sas_code,'* |','*~ |');
      sas_code=tranwrd(sas_code,' *-','~ *-');
      if scan(sas_code,1,' ') in ('libname' 'options' 'array') and indexc(sas_code,'(','"','"',')') then do;
         pos=indexc(sas_code,'()')+1;
         sas_code=tranwrd(sas_code,'" "','"~"');
         sas_code=tranwrd(sas_code,'","','",~"');
      end;
      i=1;
      do until(scan(sas_code,i,'~')='');
         if index(sas_code,'proc sql') then sql_flag=1;
         if sql_flag=1 then do;
            sas_code=tranwrd(sas_code,'select','~select');
            sas_code=tranwrd(sas_code,'from','~from');
            sas_code=tranwrd(sas_code,'where','~where');
            sas_code=tranwrd(sas_code,'having','~having');
            sas_code=tranwrd(sas_code,'order','~order');
            if index(sas_code,'quit;') then sql_flag=0;
         end;
         lv(i)=strip(scan(sas_code,i,'~'));
         i+1;
      end;
      line_out='';skip=0;
      do i=1 to dim(lv);
         if missing(lv(i)) then leave;
         line_out=strip(lv(i));
         firstword=compress(strip(scan(line_out,1,' ')),'%;');
         if lowcase(firstword) in ('mend' 'end' 'run' 'quit' 'endcomp') then do;
            indent+(-4);                   
            if lowcase(firstword) ne 'end' then skip=1;
         end;
         output;
         if lowcase(firstword) in ('do' 'data' 'proc' 'if' 'macro' 'compute') then do;
            if not(lowcase(firstword)='if' and index(line_out,'do') = 0) then
               indent+4; 
         end;
      end;
   end;
   keep sql_flag line_out pos indent skip firstword lastword lv:;
   format _character_;
run;

data _null_;
   set x0;
   where line_out ne ';';
   file "&currdir.\test_out.sas";
   if line_out =: 'options' then put ' ';
   if pos and line_out not in: ('libname' 'options' 'array') then put @pos line_out;
   else put @indent line_out;
   if skip then put ' ';
run;
